                          Приложение 10

                        Слово-декомпилятор


   ( Применимо для структур словаря, описанного в книге. Работает со
     словами типа "двоеточие".)

   : DECOD 0 2 !     ( ячейка с абсолютным адресом 2 используется в
                        качестве рабочей [счетчик слов])
           [COMPILE] '                ( определение PFA слова  XXX )
           DUP CFA @     ( определение адреса программы, на который
                           указывает CFA слова "XXX")
           ' :                              ( в стеке PFA слова ":")
           18 + =                              ( Это $COL [DOCOL] ?)
           IF                                 ( структура : NN ...;)
              58 EMIT SPACE             ( распечатка на экране  ":")
              DUP NFA        ( определение адреса имени слова  XXX )
              ID.                                    ( печать имени)
              BEGIN 0 0 !   ( ячейка с абсолютным адресом "0" также
                 используется в качестве рабочей. Это плохо, но так
                 не хочется заводить новые переменные в словаре)
                    DUP @ 2+    ( вычисление PFA очередного слова в
                                  списке-описании слова  XXX )
                    DUP ' LIT =                     ( оператор LIT?)
                    IF 1 0 !       ( если да, запись 1 в "0"-ячейку)
                    ELSE DUP ' (.") =                 ( это (.") ? )
                         IF 2 0 !          ( если да, то запись 2 в
                                             "0"-ячейку)
                         THEN
                    THEN NFA ID.    ( нахождение и распечатка имени
                          очередного слова из списка-описания "XXX")
                         1 2 +!      ( добавление I к счетчику слов)
                         2+ 0 @ 1 =         ( содержимое "0"-ячейки
                                              равно 1 ?)
                         OVER @ ABS  512 < OR ( может быть, это одна
                                           из команд ветвления ?)
                         IF DUP @ .    ( распечатка цифры, следующей
                                         за командой)
                            2+         ( переход к следующему слову)
                         THEN 0 @ 2 = ( последнее слово было (.") ?)
                         IF DUP COUNT TYPE      ( распечатка строки
                                                  символов)
                            DUP C@ EVEN +   ( вычисление указателя с
                                              учетом длины строки)
                            SPACE                ( Введение пробела)

                         THEN DUP @ 2+ ' ;S OVER =  ( очередное
                                                      слово ";S" ? )
                              SWAP ' QUIT = OR          ( "QUIT" ? )
                              2 @ 10 MOD 0= IF CR THEN
                                ( ввод кода перевода строки, если на
                                      строке уже отображено 10 слов)
              UNTIL 59 EMIT                ( распечатка символа ";")
           ELSE ." PRIMITIVE"       ( распечатка производится каждый
                раз, когда встретилось нечто отличное от : NNN...,')
           THEN CR ;

